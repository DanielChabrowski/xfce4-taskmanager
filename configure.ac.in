dnl
dnl xfce4-taskmanager - A small taskmanager based on the Xfce 4 libraries.
dnl
dnl 2005-2007 Johannes Zellner <webmaster@nebulon.de>

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([taskmanager_version_major], [0])
m4_define([taskmanager_version_minor], [5])
m4_define([taskmanager_version_micro], [0])
m4_define([taskmanager_version_build], [@REVISION@])
m4_define([taskmanager_version_tag], [git])
m4_define([taskmanager_version], [taskmanager_version_major().taskmanager_version_minor().taskmanager_version_micro()ifelse(taskmanager_version_tag(), [git], [taskmanager_version_tag()-taskmanager_version_build()], [taskmanager_version_tag()])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2006-2008
        The Xfce development team. All rights reserved.])
AC_INIT([Xfce4 Taskmanager], [taskmanager_version], [http://bugzilla.xfce.org/], [xfce4-taskmanager],
        [http://goodies.xfce.org/projects/applications/xfce4-taskmanager])
AC_PREREQ([2.50])
AC_CANONICAL_TARGET()
AC_REVISION([taskmanager_version_build])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 dist-bzip2])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()
AM_SILENT_RULES([yes])

dnl *******************************
dnl *** Check for UNIX variants ***
dnl *******************************
AC_AIX()
AC_ISC_POSIX()
AC_MINIX()

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AM_PROG_CC_C_O()
AC_PROG_LD()
AC_PROG_INSTALL()
AC_PROG_LIBTOOL()
AC_PROG_INTLTOOL()

dnl ************************************
dnl *** Checks for library functions ***
dnl ************************************
AC_DISABLE_STATIC()
AC_C_CONST()

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************.
AC_HEADER_STDC()
AC_CHECK_HEADERS([dirent.h pwd.h sys/types.h sys/stat.h sys/param.h \
                  stdio.h stdlib.h string.h unistd.h stdlib.h signal.h])

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.12.0])

dnl ***********************************
dnl ******* Check for OS family *******
dnl ***********************************
AC_MSG_CHECKING([for operating system family])
case "$target_os" in
  dragonfly*|freebsd*|netbsd*|openbsd*|darwin*)
    ac_taskmanager_os_family="bsd"
    ;;
  solaris*)
    ac_taskmanager_os_family="solaris"
    AC_CHECK_LIB([kstat], [kstat_open])
    ;;
  *)
    ac_taskmanager_os_family="linux"
    ;;
  esac
AC_MSG_RESULT([$ac_taskmanager_os_family])
AM_CONDITIONAL([OS_BSD_FAMILY], [test x"$ac_taskmanager_os_family" = x"bsd"])
AM_CONDITIONAL([OS_SOLARIS], [test x"$ac_taskmanager_os_family" = x"solaris"])
AM_CONDITIONAL([OS_LINUX], [test x"$ac_taskmanager_os_family" = x"linux"])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
BM_DEBUG_SUPPORT()

AC_OUTPUT([
Makefile
src/Makefile
po/Makefile.in
])

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "* Target OS:     $ac_taskmanager_os_family"
echo "* Debug Support: $enable_debug"
echo

